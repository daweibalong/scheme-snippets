* top-level defination
  #+begin_src scheme
  (define name
    (lambda (x)
      exp))
  (define name val)
  #+end_src
  一个过程的本质就是一个lambda表达式，而对name的绑定使得函数的定义与变量的的定义达到形势上的统一，一般的函数定义都使用上述的过程来定义。
  对于let绑定，其本质上也是一个lambda表达式.
  #+begin_src scheme
  (let ([a 10])
     (+ a 2))
  =>
  ((lambda (x) (+ x 2)) 10)
  #+end_src
* 关于语言的实现
  在语言实现层面，最开始要做的是实现语言的核心语义，之后的扩展语义都是
  利用核心语义进行的扩展，比如:所有函数形式，let等等都可以用lambda语义
  进行实现。
* lexial scope
  词法作用域是指对于函数而言，其变量的有效值为最内层的绑定。
  自由变量:occur free
  #+begin_src scheme
  (and test clause)
  =>
  (if test
      clause)
  #+end_src
* even? 与 odd?的定义
  两个函数之间互相调用的递归形势:
  #+begin_src scheme
  (define even?
    (lambda (x)
      (if (zero? x)
          #t
          (odd? (- x 1)))))
  (define odd?
    (lambda (x)
      (if (zero? x)
          #f
          (even? (- x 1)))))
  #+end_src
* lisp的基本形式
  1. () ＝> procedurer
  2. '() => quote
  3. object: string number symbol
* 模拟栈
** list
   #+begin_src scheme
(define make-stack
    (lambda ()
      (let ([ls '()])
        (lambda (msg . args)
          (case msg
            ['empty? (null? ls)]
            ['push! (set! ls (cons (car args) ls))]
            ['top (car ls)]
            ['pop! (set! ls (cdr ls))]
            ['ref (list-ref ls (car args))]
            ['set! (set-car! (list-tail ls (car args)) (cadr args))]
            [else "oops!"])))))
> (define stack1 (make-stack))
> (stack1 'push! 'a)
> (stack1 'push! 'b)
> (stack1 'push! 'c)
> (stack1 'set! 1 'd)
> (stack1 'ref 1)
d
> (stack1 'top)
c
> (stack1 'pop)
"oops!"
> (stack1 'pop!)
> (stack1 'top)
d
   #+end_src
** vector
  #+begin_src scheme
  (define make-stack
    (lambda (n)
      (let ([ls (make-vector (+ 1 n))])
        (vector-set! ls 0 0)
        (lambda (msg . args)
          (let ([len (vector-ref ls 0)])
            (case msg
              ['empty? (zero? len)]
              ['push!
               (begin 
                 (vector-set! ls (+ 1 len) (car args))
                 (vector-set! ls 0 (+ 1 len)))]
              ['top (vector-ref ls len)]
              ['pop! (vector-set! ls 0 (- len 1))]
              ['ref (vector-ref ls (- len (car args)))]
              ['set! (vector-set! ls (- len (car args)) (cadr args))]
              [else "oops!"]))))))
;;test
> (define stack1 (make-stack 5))
> (stack1 'push! 'a)
> (stack1 'push! 'b)
> (stack1 'push! 'c)
> (stack1 'ref 0)
c
> (stack1 'ref 2)
a
> (stack1 'set! 1 'd)
> (stack1 'ref 1)
d
> (stack1 'top)
c
> (stack1 'pop!)
> (stack1 'top)
d
  
  #+end_src

* list? 实现
** 我的实现
  #+begin_src scheme
  (define my-list? 
    (lambda (ls)
      (define race
        (lambda (h)
          (if (null? h)
              #t
              (if (and (pair? h) (not (eqv? h ls)))
                  (race (cdr h))
                  #f))))
      (race ls)))
  #+end_src
** hare and tortoise algorithm 实现
     对于一般正常list的判断会比我实现的版本少一半的函数调用，但对与圈
     的判断就没有多大优势了吧?
     #+begin_src scheme
(define list?
  (lambda (x)
    (let race ([h x] [t x])
      (if (pair? h)
          (let ([h (cdr h)])
            (if (pair? h)
                (and (not (eq? h t))
                     (race (cdr h) (cdr t)))
                (null? h)))
          (null? h)))))
     #+end_src

** TODO sicp中常数空间判断圈的解决方法?
* let*实现
  #+begin_src scheme 
 (define-syntax let
  (syntax-rules ()
    [(_ ((x e) ...) b1 b2 ...)
     ((lambda (x ...) b1 b2 ...) e ...)]))
  #+end_src
  #+begin_src schme
  (define-syntax my-let*
    (syntax-rules ()
      [(_ ((x e)) b1 b2 ...)
       ((lambda (x) b1 b2 ...) e)]
      [(_ ((x1 e1) (x2 e2) ...) b1 b2 ...)
       (my-let* ([x1 e1])
         (my-let* ((x2 e2) ...) b1 b2 ...))]))
  ;;test
  (my-let* ([a 5] [b (+ a a)] [c (+ a b)])
      (list a b c))
  =>(5 10 15)
  #+end_src
* continuation
  一个表达式的continuation就是外部函数要利用该表达式产生的结果做什么。
  比如:
  #+begin_src scheme
  (cons (exp) 'a)
  #+end_src
  这里面(exp)的continuation就是等待exp的结果然后将其cons到'a上，它保存
  了等待(exp)结果的计算点并进行后续计算的函数调用栈!
  在scheme中，所有的continuation都可以用call/cc(call with current
  continuation来捕捉到(call/cc必须接受一个单一参数的函数作为参数):
  #+begin_src scheme
  (call/cc
    (lambda (k)
       exp))
  #+end_src
  这里面k就是捕捉到的continuation。
  每当显式的调用k的时候，我们就可以简单的把它想成直接将
  (call/cc...)替换成传递给k的值并继续执行。比如call/cc放在递归的终止条件处，那么
  每次调用continuation的时候就会将终止返回值替换成传递给coninuation的
  值，再或者局部退出的例子(下面程序)，将call/cc放到内部函数的最外部，调用
  continuation的时候就直接结束程序了。
  下面是利用call/cc来写的product函数:
  #+begin_src scheme
(define product
  (lambda (ls)
    ;显式调用break的时候，直接跳到这里，也就是返回(call/cc。。。)的返
  回值
    (call/cc  
      (lambda (break)
        (let f ([ls ls])
          (cond
            [(null? ls) 1]
            [(= (car ls) 0) (break "zero")]
            [else (* (car ls) (f (cdr ls)))]))))))
;test
(product '(1 2 3 4 5))  => 120
(product '(7 3 8 0 1 9 5)) =>  "zero"
  #+end_src
关于那个阴阳谜题，如果用上面这种代换的方式来考虑的话，似乎就没有那么难理解
了。

* cps
  cps的有些转换还是很麻烦!!!
  1. 正常函数的返回都隐含一个continuation，就是利用这个函数的返回值来
     做的后续事情，而cps的本质就是将这个隐式的continuation显式的当做参
     数传递进去，并在函数中完成应有的continuation并将最终结果返回。
  2. 这跟尾递归似乎很像，在改造递归为尾递归的时候，就将当前状态通过accumulator汇集到函数内部的操作，当达到结束条件时返回汇集结果，而不必再返回来收集递归过程中的返回值。
  3. cps似乎就是同样的道理，每次将continuation传递到内部进行操作的组合，当达到底部的时候直接将汇集的continuation的计算结果返回，而不必返回来再去计算每一步的continuation。
** Exercise 3.4.3
Rewrite the following expression in CPS to avoid using call/cc.
#+begin_src scheme
(define reciprocals
  (lambda (ls)
    (call/cc
      (lambda (k)
        (map (lambda (x)
               (if (= x 0)
                   (k "zero found")
                   (/ 1 x)))
             ls)))))
(reciprocals '(2 1/3 5 1/4))  (1/2 3 1/5 4)
(reciprocals '(2 1/3 0 5 1/4))  "zero found"
#+end_src 
这道题是要用CPS的形式重写上面的函数，就是实现局部退出的功能。如果不规
定用map函数来写的话，我们可以按照之前product的CPS形式的写法来写
#+begin_src scheme
(define reciprocals
    (lambda (ls k)
      (let ([break k])
        (let f ([ls ls] [k k])
          (cond
            [(null? ls) (k '())]
            [(= (car ls) 0) (break "zero!")]
            [else (f (cdr ls)
                     (lambda (x)
                       (k (cons 
                            (/ 1 (car ls))
                            x))))])))))
#+end_src
但如果不改变原来实现的方式，也就是用map函数和CPS来实现的话就会相对麻烦，
如果对CPS和Continuation理解不透彻的话会非常别扭，而且难以理解。Kent在
书中也给了提示:让我们看一下map1的实现，也就是说我们要实现一个可以传递
continuation的map，然后在转换函数中选择不同的continuation来完成异常情
况的检查与退出。
先看一下map1的实现:
#+begin_src scheme
(define map1
  (lambda (p ls)
    (if (null? ls)
        '()
        (cons (p (car ls))
              (map1 p (cdr ls))))))
#+end_src
在这里让人比较迷惑的是在非尾部调用的过程中，cons会等待p 和 map1两个函数的结果
来进行后续的计算，也就是存在两个延续，那要怎么安排才好呢?我们看一下
tspl中的原话:
#+begin_quote
When one procedure invokes another via a nontail call, the called procedure receives an implicit continuation that is responsible for completing what is left of the calling procedure's body plus returning to the calling procedure's continuation. If the call is a tail call, the called procedure simply receives the continuation of the calling procedure.
#+end_quote
也就是说，非尾部调用的时候，被调用函数会隐式的接收到一个continuation，
这个continuation代表着"完成调用函数其他部分加上被调用函数的返回值". 如
果我们按照从右往左的计算顺序，那么我们可以对map1进行第一步改造:将隐式传递
给内部map1的continuation显式的写出来:
#+begin_src scheme
(define map1
  (lambda (p ls k)
    (if (null? ls)
	(k '())
	(map1 p 
	      (cdr ls)
	      (lambda (v)
		(cons (p (car ls)) v))))))
#+end_src
然后我们看到函数p同样是一个非尾部调用，那我们进一步将传递给p的隐式的
continuation显式的写出来:
#+begin_src scheme
(cons (p (car ls)) v)
=> (p (car ls) 
      (lambda (n)
         (k (cons n v))))
#+end_src
所以可以得到map1的CPS版本:
#+begin_src scheme
(define map1
  (lambda (p ls k)
    (if (null? ls)
	(k '())
	(map1 p 
	      (cdr ls)
	      (lambda (v)
		(p (car ls) 
		   (lambda (n)
		     (k (cons n v)))))))))
#+end_src
在这里，函数p接受continuation，这个continuation是执行将p的结果cons到
map1的结果中，而在p函数中会根据是否产生异常来决定是使用这个
continuation还是退出的continuation。又因为map1在 reciprocals 中是一个
尾部调用，所以直接传递延续即可，所以reciprocals的任务就是向map1中
传递正确的p函数、延续以及退出的延续(就是map1开始前的延续):
#+begin_src scheme
(define reciprocals
  (lambda (ls k)
    (let ([break k])
      (map1
       (lambda (x k)
	 (if (= x 0)
	     (break "error")
	     (k (/ 1 x))))
       ls
       k))))
#+end_src
测试结果:
#+begin_src scheme
(reciprocals '(2 1/3 5 1/4) (lambda (x) x)) =>  (1/2 3 1/5 4)
(reciprocals '(2 1/3 0 5 1/4) (lambda (x) x)) => "error"
#+end_src
最后把map1写成内部定义的形式:
#+begin_src scheme
(define reciprocals
    (lambda (ls k)
      (let ([break k])
        (let map1 ([p (lambda (x k)
                        (if (= x 0)
                            (break "error")
                            (k (/ 1 x))))]
                   [ls ls]
                   [k k])
          (if (null? ls)
              (k '())
              (map1
                p
                (cdr ls)
                (lambda (v)
                  (p (car ls) 
                     (lambda (n)
                       (k (cons n v)))))))))))
#+end_src
